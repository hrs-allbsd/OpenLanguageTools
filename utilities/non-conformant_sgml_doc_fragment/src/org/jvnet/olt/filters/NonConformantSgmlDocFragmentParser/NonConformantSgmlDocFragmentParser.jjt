
/*
 * Copyright  2005 Sun Microsystems, Inc. 
 * All rights reserved Use is subject to license terms.
 *
 */

options{
  NODE_PACKAGE = "org.jvnet.olt.filters.NonConformantSgmlDocFragmentParser";
  LOOKAHEAD=3;
  VISITOR = true;
  FORCE_LA_CHECK = true;
  STATIC = false;
  UNICODE_INPUT = true;
}

PARSER_BEGIN(NonConformantSgmlDocFragmentParser)

package org.jvnet.olt.filters.NonConformantSgmlDocFragmentParser;
import org.jvnet.olt.parsers.tagged.*;


public class NonConformantSgmlDocFragmentParser
{
  protected boolean boolParsed = false;

  /**
   *  @exception  ParseException
   */
  public void parse()
    throws ParseException
  {
    //  Call top level rule.
    file();
    boolParsed = true;
  }

  /**
   *  walkParseTree:  This method provides an interface to allow node Visitors
   *  to be passed to the parse tree generated by this parser. 
   *  @param  visitor The visitor to act on all the nodes in the parse tree.
   *  @param  data    An object to be used as an aid to the tree walk.
   *  @exception Exception
   */
  public void walkParseTree(NonConformantSgmlDocFragmentParserVisitor visitor, Object data)
    throws Exception 
  {
    if(boolParsed && (visitor != null))
    {
      //  Get root node of the parse tree
      SimpleNode node =(SimpleNode) jjtree.rootNode();
      node.jjtAccept(visitor, data);
    }
    else
    {
      //  Throw an exception
      throw new Exception("Input stream not parsed");
    }
  }

/**
   *  walkParseTree:  This method provides an interface to allow node Visitors of a generic type
   *  to be passed to the parse tree generated by this parser. 
   *  @param  visitor The visitor to act on all the nodes in the parse tree.
   *  @param  data    An object to be used as an aid to the tree walk.
   *  @exception Exception
   */
  public void walkParseTree(TaggedMarkupVisitor visitor, Object data)
    throws Exception 
  {
    if(boolParsed && (visitor != null))
    {
      //  Get root node of the parse tree
      TaggedMarkupNode node =(TaggedMarkupNode) jjtree.rootNode();
      node.jjtAccept(visitor, data);
    }
    else
    {
      //  Throw an exception
      throw new Exception("Input stream not parsed");
    }
  }


  protected void finalize() throws Throwable
  {
    super.finalize();
  }
}

PARSER_END(NonConformantSgmlDocFragmentParser)


//  Tokens

/* JSP tags */
MORE :
{
  "<%" : IN_JSP
}

<IN_JSP>
TOKEN : 
{ <JSP: ( "%>" ) > :DEFAULT 
}

/* JSP Inline Tags */
MORE :
{
   "<%=" : IN_JSP_INLINE
}

<IN_JSP_INLINE>
TOKEN :
{ <JSP_INLINE: ( "%>"  ) >:DEFAULT
}




/*  s */
<IN_INTERNAL_SUBSET>
MORE :
{
  "<!--" : IN_SUBSET_COMMENT
}

<IN_SUBSET_COMMENT>
TOKEN :
{
  <SUBSET_COMMENT: "-->" > : IN_INTERNAL_SUBSET
}

MORE :
{
  "<!--" : IN_COMMENT
}

<IN_COMMENT>
TOKEN :
{
  <COMMENT: "--" ( " " | "\t" | "\n" | "\f" | "\b" | "\r")* ">" > : DEFAULT
}

/*  Processing Instructions */
MORE :
{
  "<?" : IN_PI
}

<IN_PI>
TOKEN :
{
  <PI: ">" > : DEFAULT
}


/*  Doctype declaration */
/* Strictly speaking, <!doctype is incorrect, but netscrape 4.x composer writes
   doctypes in lower case, so we have to deal with them */
TOKEN :
{
  <DOCTYPE: ( "<!DOCTYPE" | "<!doctype") > : DOCTYPEDECL
}

<DOCTYPEDECL>
TOKEN :
{
  <INTERNAL_SUBSET_START : "[" > : IN_INTERNAL_SUBSET
}

<IN_INTERNAL_SUBSET>
TOKEN :
{
  <INTERNAL_SUBSET_WS : ( " " | "\t" | "\n" | "\f" | "\b" | "\r")+ > 
}

<IN_INTERNAL_SUBSET>
TOKEN :
{
  <INTERNAL_SUBSET_END: "]" > : DOCTYPEDECL
}

<IN_INTERNAL_SUBSET>
MORE :
{
    "<!NOTATION" : IN_NOTATION_DECL |
    "<!ENTITY" : IN_ENTITY_DECL
}

<IN_INTERNAL_SUBSET>
TOKEN :
{
	<WEIRD_ENT : ("%") ( ~["<" , " ", "\t", "]", "\n", "\r", ";" ] )+ (";")? >
}

<IN_ENTITY_DECL>
TOKEN :
{
  <#ENT_STRING_LIT: "'" ( ~["'"] )* "'" | "\"" ( ~["\""] )* "\"" >
  | <ENTITY_DECL : ">" > : IN_INTERNAL_SUBSET
}

<IN_ENTITY_DECL>
MORE : 
{  
  <(  <ENT_STRING_LIT> | ~[">"] )    >
}

<IN_NOTATION_DECL>
TOKEN :
{

// allow simple notation declarations eg.
// <!NOTATION "Frobnitz">
// allow also stuff like <!NOTATION epsi SYSTEM \"epsi\"> 

  <#NOT_STRING_LIT: "'" ( ~["'"] )* "'" |
                     "\"" ( ~["\""] )* "\"" >
  | <NOTATION_DECL : ">" > : IN_INTERNAL_SUBSET
}

<IN_NOTATION_DECL>
MORE : 
{  
  <(  <NOT_STRING_LIT> | ~[">"] )    >
}

//----- special cases for allowing entity declarations *outside* the internal subset
<DEFAULT>
MORE :
{
    "<!NOTATION" : NONSTANDARD_IN_NOTATION_DECL |
    "<!ENTITY" : NONSTANDARD_IN_ENTITY_DECL
}

<DEFAULT>
TOKEN :
{
	<NONSTANDARD_WEIRD_ENT : ("%") ( ~["<" , " " , "\t", "]", "\n", "\r", ";" ] )+ (";")? >
}

<NONSTANDARD_IN_ENTITY_DECL>
TOKEN :
{
  <#NONSTANDARD_ENT_STRING_LIT: "'" ( ~["'"] )* "'" | "\"" ( ~["\""] )* "\""  >
|  <NON_STANDARD_ENTITY_DECL : ">" > : DEFAULT
}

<NONSTANDARD_IN_ENTITY_DECL>
MORE : 
{  
  <(  <NONSTANDARD_ENT_STRING_LIT> | ~[">"] )    >
}

<NONSTANDARD_IN_NOTATION_DECL>
TOKEN :
{
  <#NONSTANDARD_NOT_STRING_LIT: "'" ( ~["'"] )* "'" | "\"" ( ~["\""] )* "\""  >
|  <NON_STANDARD_NOTATION_DECL : ">" > : DEFAULT
}

<NONSTANDARD_IN_NOTATION_DECL>
MORE : 
{  
  <(  <NONSTANDARD_NOT_STRING_LIT> | ~[">"] )    >
}

//---- end special case for entity declarations outside the internal subset
  

<DOCTYPEDECL>
TOKEN :
{
  <DOCTYPE_CLOSE:         ">"             > : DEFAULT 
| <DOCTYPE_TEXT:          ( ~["[", ">" ] )+     >  
}



/*  CDATA Sections */
MORE :
{
  "<![CDATA[" : IN_CDATA
}

<IN_CDATA>
TOKEN :
{
  <CDATA: "]]>" > : DEFAULT
}



//  The continuation clauses for the various MORE tokens
<IN_COMMENT,
IN_JSP,
IN_JSP_INLINE,
IN_SUBSET_COMMENT,
IN_PI,
//IN_ENTITY_DECL,
IN_CDATA>
MORE :
{
  < ~[] >
}

TOKEN :
{
	<ENTITY: ("&" | "%") ( ~["<" , " " , "\t" , "\n", "\r", ";" ] )+ (";")? >
}

/*  Tags and bracket tokens */
TOKEN :
{
        <SMARKSECT:  "<!["  >
|       <EMARKSECT:  "]]>"  >
|	<STAGO:	     "<"				>	: TAG
|	<ETAGO:	     "</"				>	: TAG
|       <OSQRB:      "["  >
|       <CSQRB:      "]"  >
|       <GT:         ">"  >
|	<PCDATA:     ( ~["<", "\u0000", "&", "%", ">" , "]" , "[", "\r","\n","\b","\f"," ","\t"] )+   >
|       <WHITESPACE: ( ["\r","\n","\b","\f"," ","\t"] )+	>
|	<AMP:	     "&"  >
|	<PCT:	     "%"  > 
}

<TAG>
TOKEN :
{
    <#LETTER:   ["a"-"z","A"-"Z"]   >
|   <#NUM:		["0"-"9"]			>
|   <#NCNAMECHAR:	<LETTER> | <NUM> | "." | "-" | "_"  >
|   <TAGNAME:   ( <LETTER> | "_" ) ( <NCNAMECHAR> )*  >   : ATTLIST 
}

<TAG>
TOKEN :
{
    <PREFIX:   ( <TAGNAME> ) ( ":" ) > 
}


<TAG, ATTLIST>
TOKEN :
{
    <EMPTYTAGC:         "/>"                            >       : DEFAULT
}

<TAG, ATTLIST>
TOKEN :
{
    <TAGC:		">"				>	: DEFAULT
}

<TAG, ATTLIST>
TOKEN :
{
    <WS:		(["\r","\n","\b","\f"," ","\t"])+	>
}


<ATTLIST> TOKEN :
{  <#STRING_LIT:   "'" ( ~["'"] )* "'" | "\"" ( ~["\""] )* "\""  >
|  <ATTRIBS:       (  <STRING_LIT> | ~[ ">", "\"", "'","/" ] )+    >
}




//  Productions
void file() :
{}
{
  ( comment() | pcdata() | processing_inst() | jsp() | jsp_inline() )*
  ( doctype() )?
  ( sgml_data() | entity_decl() | notation_decl() )*
  (eof())
}

void doctype() :
{}
{
  doctype_beginning()
  ( internal_sub_set() )?
  doctype_ending()
}

void doctype_beginning() :
{
  Token t;
  String s = "";
}
{
  (
    t=<DOCTYPE> {
    s = t.image;
    jjtThis.setBeginPos(t.beginLine, t.beginColumn, false);
  }
    t=<DOCTYPE_TEXT> {
    s += t.image;
    jjtThis.setEndPos(t.endLine, t.endColumn, false);
  }
  ) 
  {  jjtThis.setNodeData(s); }
}

void doctype_ending() :
{
  Token t;
  String s = "";
}
{
  (
    ( t=<DOCTYPE_TEXT> { 
      s = t.image;
      jjtThis.setBeginPos(t.beginLine, t.beginColumn, false);
    } )?
    t=<DOCTYPE_CLOSE> { 
      s += t.image;    
      jjtThis.setBeginPos(t.beginLine, t.beginColumn, false);
      jjtThis.setEndPos(t.endLine, t.endColumn, false);
    }
  ) 
  {  jjtThis.setNodeData(s); }
}


void internal_sub_set() :
{}
{
  internal_sub_set_beginning()
  (internal_sub_set_ws_comment())? 
  (
    (entity_decl() | notation_decl() | int_entity()) (internal_sub_set_ws_comment())?
    //(entity_decl()) (internal_sub_set_ws_comment())?
  )*
  internal_sub_set_ending()
}

void internal_sub_set_beginning() :
{
  Token t;
}
{
  t=<INTERNAL_SUBSET_START> {
    jjtThis.setNodeData(t.image); 
    jjtThis.setBeginPos(t.beginLine, t.beginColumn, false);
    jjtThis.setEndPos(t.endLine, t.endColumn, false);
  }
}

void internal_sub_set_ending() :
{
  Token t;
}
{
  t=<INTERNAL_SUBSET_END> {
    jjtThis.setNodeData(t.image); 
    jjtThis.setBeginPos(t.beginLine, t.beginColumn, false);
    jjtThis.setEndPos(t.endLine, t.endColumn, false);
  }
}

void internal_sub_set_ws_comment() :
{
  Token t;
  String s = "";
}
{
     ( 
       t=<INTERNAL_SUBSET_WS>  { 
         s += t.image;   
	 jjtThis.setBeginPos(t.beginLine, t.beginColumn, false);
	 jjtThis.setEndPos(t.endLine, t.endColumn, true);
       } 
	 | 
       t=<SUBSET_COMMENT>      {  
         s += t.image;   
	 jjtThis.setBeginPos(t.beginLine, t.beginColumn, false);
	 jjtThis.setEndPos(t.endLine, t.endColumn, true);
       }
     )+
     { jjtThis.setNodeData(s);}
}

void entity_decl() :
{
  Token t;
}
{
  ( t=<ENTITY_DECL> | t=<NON_STANDARD_ENTITY_DECL> ){
    jjtThis.setNodeData(t.image); 
    jjtThis.setBeginPos(t.beginLine, t.beginColumn, false);
    jjtThis.setEndPos(t.endLine, t.endColumn, false);
  }
}

void notation_decl() :
{
  Token t;
}
{
  ( t=<NOTATION_DECL> | t=<NON_STANDARD_NOTATION_DECL> ){
    jjtThis.setNodeData(t.image); 
    jjtThis.setBeginPos(t.beginLine, t.beginColumn, false);
    jjtThis.setEndPos(t.endLine, t.endColumn, false);
  }
}

void sgml_data() :
{}
{
    (  comment()
       | jsp()
       | jsp_inline()
       | tag() 
       | pcdata() 
       | entity()
       | marked_sect() 
       | cdata() 
       | processing_inst() 
    )
}

void comment() :
{
  Token t;
}
{ 
  (t=<COMMENT>) {
    jjtThis.setNodeData(t.image); 
    jjtThis.setBeginPos(t.beginLine, t.beginColumn, false);
    jjtThis.setEndPos(t.endLine, t.endColumn, false);
  }
}

void jsp() :
{
  Token t;
}
{ // these are block jsp tags - we're setting the tagname to be "jsp_block"
  // so that we can hack nice block/inline support for jsp tags in the
  // sgml filter code. This is very sneaky (basically, we will treat these
  // tags as normal open-tags in when we've done the translation from
  // NonConformantDocFragment nodes to TaggedMarkup nodes... )
  (t=<JSP>) {
    jjtThis.setNodeData(t.image);
    jjtThis.setTagName("jsp_block");
    jjtThis.setBeginPos(t.beginLine, t.beginColumn, false);
    jjtThis.setEndPos(t.endLine, t.endColumn, false);
    jjtThis.setIsEmptyTag(true);
  }
}

void jsp_inline() :
{
  Token t;
}
{ // these are inline jsp tags - we're setting the tagname to be "jsp_inline"
  // so that we can hack nice block/inline support for jsp tags in the
  // sgml filter code. This is very sneaky (basically, we will treat these
  // tags as normal open-tags in when we've done the translation from
  // NonConformantDocFragment nodes to TaggedMarkup nodes... )
  (t=<JSP_INLINE>) {
    jjtThis.setNodeData(t.image);
    jjtThis.setTagName("jsp_inline");
    jjtThis.setBeginPos(t.beginLine, t.beginColumn, false);
    jjtThis.setEndPos(t.endLine, t.endColumn, false);
    jjtThis.setIsEmptyTag(true);
  }
}


void cdata() :
{
  Token t;
}
{
  t=<CDATA> {
    jjtThis.setNodeData(t.image); 
    jjtThis.setBeginPos(t.beginLine, t.beginColumn, false);
    jjtThis.setEndPos(t.endLine, t.endColumn, false);
  }
}

void processing_inst() :
{
  Token t;
}
{
  t=<PI> {
    jjtThis.setNodeData(t.image); 
    jjtThis.setBeginPos(t.beginLine, t.beginColumn, false);
    jjtThis.setEndPos(t.endLine, t.endColumn, false);
  }
}

void entity() :
{
  Token t;
}
{
  (t=<ENTITY> | t=<NONSTANDARD_WEIRD_ENT>) {
    jjtThis.setNodeData(t.image); 
    jjtThis.setBeginPos(t.beginLine, t.beginColumn, false);
    jjtThis.setEndPos(t.endLine, t.endColumn, false);
  }
}

void int_entity() :
{
  Token t;
}
{
  t=<WEIRD_ENT> {
    jjtThis.setNodeData(t.image);
	jjtThis.setBeginPos(t.beginLine, t.beginColumn, false);
	jjtThis.setEndPos(t.endLine, t.endColumn, false);
  }
}

void pcdata() :
{
  Token t;
  String s = "";
  boolean boolStarted = false;
}
{
  ( t=<PCDATA>   {
    s += t.image;
    if(!boolStarted) {
        boolStarted = true;
        jjtThis.setBeginPos(t.beginLine, t.beginColumn, false);
    }
    jjtThis.setEndPos(t.endLine, t.endColumn, true);
  } 
    | t=<OSQRB>  {
    s += t.image;
    if(!boolStarted) {
        boolStarted = true;
        jjtThis.setBeginPos(t.beginLine, t.beginColumn, false);
    }
    jjtThis.setEndPos(t.endLine, t.endColumn, true);
  }
    | t=<CSQRB>  {
    s += t.image;
    if(!boolStarted) {
        boolStarted = true;
        jjtThis.setBeginPos(t.beginLine, t.beginColumn, false);
    }
    jjtThis.setEndPos(t.endLine, t.endColumn, true);
  } 
    | t=<GT>     {
    s += t.image;
    jjtThis.setBeginPos(t.beginLine, t.beginColumn, false);
    jjtThis.setEndPos(t.endLine, t.endColumn, true);
  } 
    | t=<AMP>     {
    s = t.image;
    if(!boolStarted) {
        boolStarted = true;
        jjtThis.setBeginPos(t.beginLine, t.beginColumn, false);
    }
    jjtThis.setEndPos(t.endLine, t.endColumn, true);
  } 
    | t=<PCT>     {
    s += t.image;
    if(!boolStarted) {
        boolStarted = true;
        jjtThis.setBeginPos(t.beginLine, t.beginColumn, false);
    }
    jjtThis.setEndPos(t.endLine, t.endColumn, true);
  } 
    | t=<WHITESPACE>     {
    s += t.image;
    if(!boolStarted) {
        boolStarted = true;
        jjtThis.setBeginPos(t.beginLine, t.beginColumn, false);
    }
    jjtThis.setEndPos(t.endLine, t.endColumn, true);
  }
    | t=<NONSTANDARD_WEIRD_ENT> {
    s += t.image;
    if (!boolStarted) {
         boolStarted = true; 
         jjtThis.setBeginPos(t.beginLine, t.beginColumn, false);
    }
    jjtThis.setEndPos(t.endLine, t.endColumn, true);
  } 
  )+
  { jjtThis.setNodeData(s); } 
}

void marked_sect() :
{}
{
 ( marked_section_tag()
 ( sgml_data() )*
 ( end_marked_sect() )? ) | end_marked_sect()
}

String start_marked_sect() :
{
  Token t;
}
{
  t=<SMARKSECT> { return t.image; }
}

void marked_section_tag() :
{
  String s="";
  String st="";
  String sflag="";
  Token t;
}
{
  ( st=start_marked_sect()      { s += st; }
    
  ( t=<WHITESPACE>                { s += t.image; } )*

    ( sflag=marked_section_flag() { s += sflag; }
      ( t=<WHITESPACE>                { s += t.image; } )* 
    )?
  
    st=open_sqr_brkt()          { s += st; }   
  ) 
  { jjtThis.setNodeData(s); 
    if (sflag.equals("")){
        sflag="INCLUDE";
    } jjtThis.setMarkedSectFlag(sflag); }
}

String marked_section_flag() :
{
  Token t;
}
{
  ( t=<ENTITY>
  | t=<NONSTANDARD_WEIRD_ENT>
  | t=<PCDATA> ) { return t.image; }
}

String open_sqr_brkt() :
{
  Token t;
}
{
  t=<OSQRB> { return t.image; }
}

void end_marked_sect() :
{
  Token t;
}
{
  t=<EMARKSECT> { jjtThis.setNodeData(t.image); }
}

void tag() :
{}
{
  open_tag() | close_tag()
}

// XML open tag ----
void open_tag() :
{
  Token t;
  String s = "";  
  String otag = "";
}
{
  (
    t=<STAGO> { s += t.image; }
    
    (t=<WS> {s+=t.image;})?
    (t=<TAGNAME> { s += t.image; jjtThis.setTagName(t.image);} | 
     ( t=<PREFIX> { s += t.image; jjtThis.setPrefix(t.image); }
      t=<TAGNAME>) { s += t.image; jjtThis.setTagName(t.image);})
    (t=<WS> {s+=t.image;})?

    ( (t=<ATTRIBS>  { s += t.image; jjtThis.setHasAttribute(true); }
     | t=<JSP> { s+=t.image; })
     (t=<WS> {s +=t.image; })?
    )*

    (t=<TAGC> { s += t.image; jjtThis.setIsEmptyTag(false); }  
    |t=<EMPTYTAGC> { s += t.image; jjtThis.setIsEmptyTag(true); } )?  
  )
  { jjtThis.setNodeData(s); }    
}

void close_tag() :
{
  Token t;
  String s = "";  
}
{
  (
    t=<ETAGO> { s += t.image; }
    (t=<WS> {s+=t.image;})?

    ((t=<TAGNAME> { s += t.image; jjtThis.setTagName(t.image);}
    | ( t=<PREFIX> { s += t.image; jjtThis.setPrefix(t.image); }
	t=<TAGNAME>) { s += t.image; jjtThis.setTagName(t.image);})
       (t=<WS> {s+=t.image;})?

       )? /* '?' included to cover possible tag minimization */
    t=<TAGC> { s += t.image; }    
  )
  {  jjtThis.setNodeData(s); }  
}

void eof():
{}{
   <EOF> 
}

